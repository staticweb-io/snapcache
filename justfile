repo_root := `pwd`
svn_dir := "./wp-org-svn"
svn_user := "staticwebio"
wordpress_dir := "./dev/data/wordpress1"

alias b := build
alias fmt := format
alias t := test
alias u := update
alias w := watch-dev

[private]
list:
    @# First command in the file is invoked by default
    @just --list

# Build plugin zip for GitHub release
build:
    nix build .#plugin

# Build plugin zip for wordpress.org release
build-wp-org:
    nix build .#pluginWpOrg

# Run tests and other checks
check: _check_no_test test

_check_no_test: _lint _validate _phpcs
    php ./vendor/bin/rector --debug --dry-run

# Run development server
[working-directory('dev')]
dev CLEAN="false" DEBUG="false":
    {{ if CLEAN == "true" { "rm -rf data" } else { "" } }}
    {{ if DEBUG == "true" { "ENABLE_XDEBUG=true nix run . --impure -- --no-server" } else { "nix run . -- --no-server" } }}

# Format source and then check for unfixable issues
format: && _format-php
    just --fmt --unstable
    fd -e json -x jsonfmt -w
    fd -e nix -x nixfmt

_format-php:
    just _phpcbf || true && just _phpcs

_lint:
    php ./vendor/bin/parallel-lint src

_phpcbf:
    php ./vendor/bin/phpcbf -d memory_limit=512M --standard=./phpcs.xml --extensions=php src tests *.php || true

_phpcs:
    php ./vendor/bin/phpcs -d memory_limit=512M -s --standard=./phpcs.xml --extensions=php src tests *.php

# Run rector code transformations
rector: && _phpcbf
    # We sometimes get errors running without --debug
    php ./vendor/bin/rector --debug

# Checkout WordPress.org subversion repo
svn-checkout:
    svn co https://plugins.svn.wordpress.org/snapcache "{{ svn_dir }}"

# Release latest source build using version in update.json
svn-release:
    just _svn-sync-trunk "$(nix build .#pluginWpOrgSrc --print-out-paths)"
    just _svn-release-version "$(jq -r .version update.json)"

_svn-release-version VERSION:
    svn cp "{{ svn_dir }}"/trunk "{{ svn_dir }}"/tags/"{{ VERSION }}"
    svn ci "{{ svn_dir }}" --username "{{ svn_user }}" -m "tagging version {{ VERSION }}"

_svn-sync-trunk SRC:
    rsync -av --delete --chmod=F664,D775 --owner "{{ SRC }}"/* "{{ svn_dir }}"/trunk/
    svn add --force "{{ svn_dir }}"/trunk/*

# Update subversion repo from WordPress.org
svn-update:
    svn up "{{ svn_dir }}"

# Validate and run tests in a sandbox
test: _validate _phpcs _test-integration

# Run integration tests in a sandbox
_test-integration:
    nix flake check ./dev

# Run integration tests against a live dev server
_test-integration-live:
    WORDPRESS_DIR="$(realpath {{ wordpress_dir }})" php ./vendor/bin/phpunit --testsuite Integration

# Run tests against a live dev server
test-live: _test-integration-live

# Test against github:staticweb-io/wordpress-flake#prerelease
test-wordpress-prerelease:
    #!/usr/bin/env bash
    set -euo pipefail
    rev=$(nix flake metadata ./dev --json 2>/dev/null | jq -r '.locks.nodes["wordpress-flake"].locked.rev')
    version=$(nix eval "github:staticweb-io/wordpress-flake/$rev#prerelease.version" --raw 2>/dev/null)
    echo "Testing WordPress prerelease version $version"
    WORDPRESS_PACKAGE=prerelease nix flake check --impure ./dev

_update-composer-deps: && update-hashes
    composer update

# Upgrade dependencies
update: _update-flakes _update-composer-deps

_update-flakes:
    nix flake update
    fd flake.nix -j 4 -x bash -c 'echo "Updating flake inputs in {//}"; cd "{//}" && nix flake update --inputs-from "$0"' "{{ repo_root }}"

# Update the vendorHash after a composer.json change
update-hashes:
    ./bin/update-hashes

# Update the update.json file with current values
update-json:
    ./bin/update-json

_validate:
    composer validate --strict

_watch-dev-cmd: format _validate _test-integration

# Run formatters and dev server tests when files change
watch-dev:
    @watchexec --exts json,nix,php --on-busy-update=queue --stdin-quit -- just _watch-dev-cmd
